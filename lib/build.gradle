apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'net.ltgt.errorprone'
// discover dependency current versions ./gradlew dependencyUpdates -Drevision=release
apply plugin: 'com.github.ben-manes.versions'

apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/android.gradle")

kapt {
    correctErrorTypes = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlin

    implementation deps.design
//    implementation deps.calligraphy

    api deps.recyclerview
    api deps.rxjava
    api deps.rxjava_android
    api deps.rxjava_kotlin
    api deps.timber
    implementation deps.rxbindings_design_kt
    implementation deps.rxbindings_appcompat_kt
    implementation deps.rxbindings_support_kt

    api deps.lifecycle_ext
    api deps.navigation_fragment
    api deps.navigation_ui

    api deps.dagger
    api deps.dagger_android
    api deps.autodagger

    kapt deps.dagger_compiler
    kapt deps.autodagger_compiler
    compileOnly deps.javax_annotations

    testImplementation deps.junit

    androidTestImplementation(deps.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

// build a jar with source files
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError  false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}